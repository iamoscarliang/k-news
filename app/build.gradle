plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'androidx.navigation.safeargs.kotlin'
    id 'kotlin-kapt'
    id 'kotlin-allopen'
}

allOpen {
    // Allow mocking for classes w/o directly opening them for release builds
    annotation 'com.oscarliang.knews.testing.OpenClass'
}

android {
    namespace 'com.oscarliang.knews'
    compileSdk 34

    defaultConfig {
        applicationId "com.oscarliang.knews"
        minSdk 21
        targetSdk 34
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.oscarliang.knews.util.NewsTestRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        dataBinding true
    }
    sourceSets {
        androidTest.java.srcDirs += "src/test-common/java"
        test.java.srcDirs += "src/test-common/java"
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'

    // Shimmer effect
    implementation 'com.facebook.shimmer:shimmer:0.5.0'

    // SwipeRefreshLayout
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.16.0'

    // Retrofit
    def retrofit_version = "2.9.0"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // Room
    def room_version = "2.6.1"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Navigation component
    def nav_version = "2.7.7"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // Dagger
    def dagger_version = "2.48.1"
    implementation "com.google.dagger:dagger:$dagger_version"
    kapt "com.google.dagger:dagger-compiler:$dagger_version"

    // Dagger android
    implementation "com.google.dagger:dagger-android:$dagger_version"
    implementation "com.google.dagger:dagger-android-support:$dagger_version"
    kapt "com.google.dagger:dagger-android-processor:$dagger_version"

    // Mock webserver test
    implementation 'com.squareup.okhttp3:mockwebserver:4.12.0'

    // Unit test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'

    // Espresso test
    def espresso_version = "3.5.1"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

    // Mockito test
    def mockito_version = "5.7.0"
    testImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestImplementation "org.mockito:mockito-android:$mockito_version"

    // Mockito-kotlin test
    def mockitoKotlin_version = "4.1.0"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlin_version"
    androidTestImplementation "org.mockito.kotlin:mockito-kotlin:$mockitoKotlin_version"

    // Architecture components test
    def androidx_arch_test_version = "2.2.0"
    testImplementation "androidx.arch.core:core-testing:$androidx_arch_test_version"
    androidTestImplementation "androidx.arch.core:core-testing:$androidx_arch_test_version"

    // Fragment test
    def fragment_version = "1.6.2"
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"
}

// We need all open to run tests which we enable only for debug builds.
project.tasks.configureEach {
    if (it.name == "testReleaseUnitTest") {
        it.enabled = false
    }
}